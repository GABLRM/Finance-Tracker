plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '3.0.1'
}

group 'com.example'
version '1.0.0'
project.description="Finance Tracker Java Application."

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.9.2'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.example.financemanager'
    mainClass = 'com.example.financemanager.FinanceTrackerApplication'
}

javafx {
    version = '21-ea+24'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
    implementation("org.xerial:sqlite-jdbc:3.44.1.0")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Finance-Tracker'
    }
    addExtraDependencies('slf4j')
    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'

        icon = "src/main/resources/com/example/financemanager/assets/launcher.$imgType"
        installerOptions += [
                '--description', 'Finance Tracker Application',
                '--app-version', project.version
        ]
        if (currentOs.linux) {
            installerType = 'deb'
            imageOptions += ['--icon', icon]
            installerOptions += ['--linux-menu-group', 'Office', '--linux-shortcut']
        }

        if (currentOs.windows) {
            installerType = 'exe'
            imageOptions += ['--icon', icon]
            installerOptions += [
                    '--win-per-user-install',
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-shortcut',
                    '--win-shortcut-prompt'
            ]
        }

        if (currentOs.macOsX) {
            installerType = 'pkg'
            imageOptions += ['--icon', icon]
            installerOptions += ['--mac-package-identifier', 'com.example.financemanager']
        }
    }
}

jlinkZip {
    group = 'distribution'
}